plugins {
    id 'org.quiltmc.loom' version '1.2.+'
    id 'maven-publish'
}

archivesBaseName = "${mod_id}-common-${minecraft_version}"

loom {
    runConfigs.configureEach {
        ideConfigGenerated = false
    }

    accessWidenerPath = file("src/main/resources/sherdsapi.accesswidener")
}

configurations {
    testCompileOnly.extendsFrom(compileOnly)
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
    }

    //DO NOT USE IN PROJECT
    //Provides necessary stuff for project to compile
    modCompileOnly("org.quiltmc:quilt-loader:${quilt_loader_version}")

    compileOnly group:'org.spongepowered', name:'mixin', version:'0.8.5'
}

sourceSets.main.resources.srcDir './src/generated/resources'

import net.fabricmc.loom.task.AbstractRemapJarTask
tasks.withType(AbstractRemapJarTask).each {
    it.targetNamespace = "named"
}

processResources {
    def buildProps = project.properties.clone()

    filesMatching(['pack.mcmeta']) {

        expand buildProps
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
    }
}