plugins {
    id 'sherdsapi-common'
    id 'net.neoforged.moddev' version "${mdg_version}"
}


neoForge {
    neoFormVersion = "${minecraft_version}-20240613.152323"

    parchment {
        minecraftVersion = parchment_minecraft_version
        mappingsVersion = parchment_version
    }

    // Required for common ATs to work. This line can be removed when ModDevGradle updates NFRT
    neoFormRuntime {
        version = '0.1.53'
    }

    def at = 'src/main/resources/META-INF/accesstransformer.cfg'
    if (file(at).exists()) {
        accessTransformers = [at]
    }
}

dependencies {
    compileOnly group:'net.fabricmc', name:'sponge-mixin', version:mixin_version
    implementation "io.github.llamalad7:mixinextras-common:${mixinextras_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

    implementation("dev.thomasglasser.tommylib:tommylib-common-${minecraft_version}:${tommylib_version}") {
        transitive = false
    }
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    sourceSets.main.resources.sourceDirectories.files.each {
        commonResources it
    }
}